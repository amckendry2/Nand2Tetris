/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    ZeroOrNegate16(in=x, z=zx, n=nx, out=x2);
    ZeroOrNegate16(in=y, z=zy, n=ny, out=y2);
    Add16(a=x2, b=y2, out=Added);
    And16(a=x2, b=y2, out=Anded);
    Mux16(a=Anded, b=Added, sel=f, out=SubResult);
    Not16(in=SubResult, out=NegatedResult);
    Mux16(a=SubResult, b=NegatedResult, sel=no, out=out, out[0..7]=Half1, out[8..15]=Half2, out[15]=ng);
    Or8Way(in=Half1, out=NotZero1);
    Or8Way(in=Half2, out=NotZero2);
    Or(a=NotZero1, b=NotZero2, out = NotZero);
    Not(in=NotZero, out=zr);
}
